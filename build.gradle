plugins {
    id "java"
    id "maven-publish"
    id "signing"
}

base {
    archivesName = project.maven_name
}

configurations {
    api.extendsFrom library
}

dependencies {
    library project(":yabg-api")
    library project(":yabg-core")
}

allprojects {
    apply plugin: "java"

    base {
        version = project.maven_version
        group = project.maven_group
    }

    repositories {
        mavenCentral()
        maven {
            name = "PaperMC"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
    }

    java {
        withSourcesJar()

        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

jar {
    // Add all dependencies which are included using "library" to the jar file and exclude the META-INF folder
    dependsOn configurations.library
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.library.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    // Rename the project's license file to LICENSE_<project_name> to avoid conflicts
    from("LICENSE") {
        rename {
            "${it}_${project.archivesBaseName}"
        }
    }

    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = "https://maven.lenni0451.net/" + (project.version.endsWith("SNAPSHOT") ? "snapshots" : "releases")

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
        maven {
            name = "ossrh"
            def releasesUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = project.version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = project.archivesBaseName
            groupId = project.group
            version = project.version

            from components.java

            pom {
                name = artifactId
                description = "Minimalistic OneBlock SkyBlock implementation for Paper (and Spigot)."
                url = "https://github.com/FlorianMichael/YetAnotherBlockGame"
                licenses {
                    license {
                        name = "Apache-2.0 license"
                        url = "https://github.com/FlorianMichael/YetAnotherBlockGame/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "FlorianMichael"
                        name = "EnZaXD"
                        email = "florian.michael07@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/FlorianMichael/YetAnotherBlockGame.git"
                    developerConnection = "scm:git:ssh://github.com/FlorianMichael/YetAnotherBlockGame.git"
                    url = "github.com/FlorianMichael/YetAnotherBlockGame"
                }
            }
        }
    }
}

signing {
    setRequired false
    sign configurations.archives
    sign publishing.publications.maven
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.dependsOn(project.tasks.withType(Sign))
}
